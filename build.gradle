/*
 * Copyright 2021 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'

apply from: "${project.scripts_url}/${project.scripts_branch}/build-quality.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/release-commons.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/signing.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/jacoco.gradle"

project.ext.limits = [
        'instruction': 30,
        'branch'     : 25,
        'line'       : 30,
        'complexity' : 26,
        'method'     : 30,
        'class'      : 35
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'Spock framework agent'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    api "com.epam.reportportal:client-java:${client_version}"
    api "com.epam.reportportal:commons-model:${commons_version}"
    api "org.spockframework:spock-core:${spock_version}"
    api 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'com.github.reportportal:agent-java-test-utils:3d4d206'
    testImplementation 'org.aspectj:aspectjweaver:1.9.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.0.3'
    testImplementation ("org.junit.platform:junit-platform-runner:${junit5_launcher_version}") {
        exclude module: 'junit'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit5_version}"
}

test {
    outputs.upToDateWhen { return false }
    useJUnitPlatform()
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    forkEvery(1)
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    environment "AGENT_NO_ANALYTICS", "1"
    testLogging.exceptionFormat = 'full'
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

build.dependsOn jacocoTestReport
