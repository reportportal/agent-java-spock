/*
 * Copyright 2021 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'java-library'

apply from: "${project.scripts_url}/${project.scripts_branch}/build-quality.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/release-commons.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/signing.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/jacoco.gradle"

project.ext.limits = [
        'instruction': 40,
        'branch'     : 40,
        'line'       : 40,
        'complexity' : 40,
        'method'     : 40,
        'class'      : 40
]

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    api "com.epam.reportportal:client-java:5.4.3"

    compileOnly "org.spockframework:spock-core:${spock_version}"
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    testImplementation "org.spockframework:spock-core:${spock_version}"
    testImplementation 'org.codehaus.groovy:groovy:2.5.14'
    testImplementation 'com.epam.reportportal:agent-java-test-utils:0.1.0'

    testImplementation 'org.aspectj:aspectjweaver:1.9.19'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation "org.mockito:mockito-core:${mockito_version}"
    testImplementation "org.mockito:mockito-inline:${mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockito_version}"
    testImplementation 'ch.qos.logback:logback-classic:1.5.18'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.4.0'
    testImplementation ("org.junit.platform:junit-platform-runner:${junit5_launcher_version}") {
        exclude module: 'junit'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit5_version}"
    testImplementation 'com.squareup.okhttp3:okhttp:4.12.0'
}

test {
    outputs.upToDateWhen { return false }
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    forkEvery(1)
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    environment "AGENT_NO_ANALYTICS", "1"
    testLogging.exceptionFormat = 'full'
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

build.dependsOn jacocoTestReport
