apply plugin: 'maven-publish'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

javadoc() {
  failOnError = false
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId 'com.epam.reportportal'
      artifactId "$project.name"
      artifact sourcesJar
      artifact javadocJar
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  pkg {
    repo = 'rp'
    name = "$project.name"
    desc = "$project.description"
    websiteUrl = 'https://reportportal.github.io/'
    issueTrackerUrl = "https://github.com/andreifinski/$project.name/issues"
    vcsUrl = "https://github.com/andreifinski/$project.name"
    githubRepo = "andreifinski/$project.name"
    githubReleaseNotesFile = 'CHANGELOG.md'
    userOrg = 'sandbox-af'
    licenses = ['GPL-3.0']
  }
}
afterReleaseBuild.dependsOn bintrayUpload
